buildscript {
	repositories {
		jcenter()
		maven {
			name = "Fabric"
			url = "http://maven.modmuss50.me"
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.1.0-SNAPSHOT"
	}
}

plugins {
	id "scala"
	id "idea"
	id "maven"
	id "maven-publish"
	id "com.github.johnrengelman.shadow" version "2.0.4"
	id "net.minecrell.licenser" version "0.4.1"
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

apply plugin: net.fabricmc.loom.LoomGradlePlugin

archivesBaseName = modid

sourceCompatibility = 1.8
targetCompatibility = 1.8


def baseVersion = version
def ENV = System.getenv()
version = baseVersion + "." + (ENV.BUILD_NUMBER ?: "local")

license {
	header file("HEADER")
	include "**/*.scala"
}

repositories {
	jcenter()
}

configurations {
	shadow
	compile.extendsFrom shadow
}

dependencies {
	minecraft "com.mojang:minecraft:1.14 Pre-Release 1"
	mappings "net.fabricmc:yarn:1.14 Pre-Release 1+build.6"
	modCompile "net.fabricmc:fabric-loader:0.4.0+build.121"

	//These are the deps included with the scala adapter, if you think you have a good dep that would be a nice fit please open an issue.
	shadow "org.scala-lang:scala-library:${scala_version}"
	shadow "org.scala-lang:scala-reflect:${scala_version}"
	shadow "org.scala-lang:scala-compiler:${scala_version}"
	shadow "org.scala-lang.modules:scala-swing_2.12:${scala_swing_version}"
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		include 'mod.json'
		expand version: project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mod.json'
	}
}

shadowJar {
	classifier = "fat"
	configurations = [project.configurations.shadow]
	exclude("META-INF")
}

tasks.build.dependsOn shadowJar


publishing {
	publications {
		maven(MavenPublication) {
			groupId project.group
			artifactId project.archivesBaseName
			version project.version

			from components.java

			artifact shadowJar {
				classifier "fat"
			}
		}
	}
	repositories {
		maven {
			url "http://mavenupload.modmuss50.me/"
			if (project.hasProperty('mavenPass')) {
				credentials {
					username 'buildslave'
					password project.getProperty('mavenPass')
				}
			}
		}
	}
}

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '309275'
		changelog = 'A changelog can be found at https://github.com/FabricMC/fabric-language-scala/commits/master'
		releaseType = 'release'
		addGameVersion '1.14-Snapshot'
		mainArtifact(shadowJar) {
			displayName = "Fabric Language Scala $baseVersion build $ENV.BUILD_NUMBER"
		}
	}
	options{
		forgeGradleIntegration = false
	}
}
