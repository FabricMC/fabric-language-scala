plugins {
    id "java"
    id "scala"
    id "maven"
    id "maven-publish"
    id 'fabric-loom' version '0.2.3-SNAPSHOT'
    id "com.github.ben-manes.versions" version "0.21.0"
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "com.matthewprenger.cursegradle" version "1.2.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
group = project.maven_group

def baseVersion = project.mod_version
def ENV = System.getenv()
version = baseVersion + "." + (ENV.BUILD_NUMBER ?: "local")

repositories {
    jcenter()
    maven {
        url = "http://maven.fabricmc.net/"
        name = "Fabric"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    //These are the deps included with the scala adapter, if you think you have a good dep that would be a nice fit please open an issue.
    shadow "org.scala-lang:scala-library:${project.scala_version}"
    shadow "org.scala-lang:scala-reflect:${project.scala_version}"
    shadow "org.scala-lang:scala-compiler:${project.scala_version}"
    shadow "org.scala-lang.modules:scala-swing_2.12:${project.scala_swing_version}"

    testCompile "org.scala-lang:scala-library:${project.scala_version}"
}

processResources {
    inputs.properties "version": project.version,
            "archives_base_name": project.archives_base_name

    from(sourceSets.main.resources.srcDirs) {
        include 'fabric.mod.json'
        expand "version": project.version,
                "archives_base_name": project.archives_base_name
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    exclude("META-INF")
}

tasks.remapJar {
    delegate.dependsOn tasks.shadowJar
    delegate.jar = shadowJar.archivePath
}


publishing {
    publications {
        maven(MavenPublication) {
            artifact(remapJar.jar) {
                builtBy remapJar
            }
        }
    }
    repositories {
        maven {
            url "http://mavenupload.modmuss50.me/"
            if (project.hasProperty('mavenPass')) {
                credentials {
                    username 'buildslave'
                    password project.getProperty('mavenPass')
                }
            }
        }
    }
}

curseforge {
    if (project.hasProperty('curse_api_key')) {
        apiKey = project.getProperty('curse_api_key')
    }
    project {
        id = '309275'
        changelog = 'A changelog can be found at https://github.com/FabricMC/fabric-language-scala/commits/master'
        releaseType = 'release'
        addGameVersion '1.14-Snapshot'
        mainArtifact(remapJar) {
            displayName = "Fabric Language Scala $baseVersion build $ENV.BUILD_NUMBER"
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/ideconfig.gradle'

license {
    header file("HEADER")
    include "**/*.scala"
}
